/************************************************************************************************
Subroutine qui créer une table de calendrier contenant les champs :
- Date
- Annee
- Mois

Paramètres
- vTableMinMax : Nom de la table dans laquelle trouver la plus petite et la plus grande date
- vFieldDateMinMax : Nom du champ contenant les dates sur lesquelles construire un calendrier
- vFieldDateKey : Alias attribué au champ contenant un Autonumber de la date dans le calendrier
- vFieldDateSuffix : Suffixe aux alias des champs dans le calendrier 
- vTableCalendar : Nom de la table résultante contenant le calendrier 

Préconditions
- Une table « Temp » n'existe pas
- Les variables vMinDate et vMaxDate ne sont pas initialisées

************************************************************************************************/
Sub sub_calendrier(vTableMinMax, vFieldDateMinMax, vFieldDateKey, vFieldDateSuffix, vTableCalendar)

	// -------------------------------------------------------------------------------------------
    // Préconditions
    // -------------------------------------------------------------------------------------------
    If Not IsNull(TableNumber('Temp')) Then
    	Call ThrowException('La table Temp existe déjà, mais la subroutine sub_calendrier en a besoin.');
    ElseIf Not IsNull(vMinDate) Then
    	Call ThrowException('La variable vMinDate est initialisée, mais la subroutine sub_calendrier en a besoin.');
    ElseIf Not IsNull(vMaxDate) Then
    	Call ThrowException('La variable vMaxDate est initialisée mais la subroutine sub_calendrier en a besoin.');
    End If
    
    // -------------------------------------------------------------------------------------------
    // Génération du calendrier 
    // -------------------------------------------------------------------------------------------
    
    // Retrouver la plus petite et la plus grande date
    Temp:
    Load
        Min([$(vFieldDateMinMax)]) as MinDateCreation,
        Max([$(vFieldDateMinMax)]) as MaxDateCreation
    Resident [$(vTableMinMax)];

    // Assigner la plus petite et plus grande date à des variables
    Let vMinDate = Num(Peek('MinDateCreation', 0, 'Temp'));
    Let vMaxDate = Num(Peek('MaxDateCreation', 0, 'Temp'));

    // Détruire la table temporaire
    Drop Table Temp;

    // Création du calendrier contenant toutes les dates
    // et load antérieur afin de définir les champs en lien
    // avec les dates
    Calendrier:
    Load 
        AutoNumber(Date) as [$(vFieldDateKey)],
        [Date] as [Date$(vFieldDateSuffix)],
        Year([Date]) as [Annee$(vFieldDateSuffix)],
        Month([Date]) as [Mois$(vFieldDateSuffix)]
    ;
    Load
        Date($(vMinDate) + IterNo() - 1) as [Date]
    AutoGenerate 1 While $(vMinDate) + IterNo() - 1 <= $(vMaxDate);

    // Détruire les variables temporaire
    Let vMinDate =;
    Let vMaxDate =;
End Sub;
